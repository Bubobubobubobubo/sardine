// A pattern is a succession of things ////////////////////////////////////////

?start: pattern
pattern: sum ("," sum?)* -> return_pattern


///////////////////////////////////////////////////////////////////////////////

// Rules concerning numbers ///////////////////////////////////////////////////

?sum: product
    | product "+" sum                  -> addition
    | product "-" sum                  -> substraction
    | product "%" sum                  -> modulo

?product: atom
     | product "*" atom                -> multiplication
     | product "/" atom                -> division
     | product "//" atom               -> floor_division
     | product "|" atom                -> choice
     | product ":" atom                -> random_in_range
     | product "!" atom                -> extend
     | product "!!" atom               -> extend_repeat

?atom: NUMBER                                              -> number
     | col
     | patname
     | "-" atom                                            -> negation
     | "$"                                                 -> get_time
     | "$" "." "m"                                         -> get_measure
     | "$" "." "p"                                         -> get_phase
     | "r"                                                 -> get_random_number
     | "i" "." LETTER                                      -> get_iterator
     | "i" "." LETTER "=" sum                              -> set_iterator
     | "i" "." LETTER "=" "[" sum "," sum "]"              -> set_iterator_step
     | "i" "." LETTER "." "reset"                          -> reset_iterator
     | "v" "." LETTER                                      -> get_variable
     | "v" "." LETTER "=" sum                              -> set_variable
     | "v" "." LETTER "." "reset"                          -> reset_variable
     | "T" "." "U"                                         -> get_unix_time
     | "T" "." "Y"                                         -> get_year
     | "T" "." "M"                                         -> get_month
     | "T" "." "D"                                         -> get_day
     | "T" "." "h"                                         -> get_hour
     | "T" "." "m"                                         -> get_minute
     | "T" "." "s"                                         -> get_second
     | "T" "." "µ"                                         -> get_microsecond
     | "{" sum "," sum "}"                                 -> generate_ramp
     | "{" sum "," sum "," sum  "}"                        -> generate_ramp_with_range
     | "[" sum ("," sum)* ","? "]"                         -> make_list
     | "c" "(" sum ")"                                     -> cosinus
     | "s" "(" sum ")"                                     -> sinus
     | "t" "(" sum ")"                                     -> tangente
     | SILENCE+                                            -> silence
     | "(" sum ")"


///////////////////////////////////////////////////////////////////////////////

// Rules concerning names /////////////////////////////////////////////////////

?patname: compose_name
    | compose_name ":" sum                -> assoc_sp_number

?compose_name: name
    | compose_name "/" name               -> specify_address

?name: NAME                               -> name

///////////////////////////////////////////////////////////////////////////////

// Rules about notes /////////////////////////////////////////////////////////

?col : note                                         -> id
     | note "->" name                               -> add_qualifier
     | col ":" name                                 -> add_modifier

note: NOTE              -> make_note
    | note "b"          -> note_flat
    | note "#"          -> note_sharp
    | note NUMBER       -> note_set_octave
    | note "'"          -> note_octave_up
    | note "."          -> note_octave_down

///////////////////////////////////////////////////////////////////////////////
     
// Tokens /////////////////////////////////////////////////////////////////////

// Notes start with an uppercase letter, other names start with a lower case letter.
// This helps disambiguating the syntax.
NOTE: "A"|"La"|"B"|"Si"|"Ti"|"D"|"Re"|"Ré"|"C"|"Do"|"E"|"Mi"|"F"|"Fa"|"G"|"Sol"
NAME: LCASE_LETTER (LETTER|DIGIT+)*

SILENCE: "."

%import common.LCASE_LETTER
%import common.UCASE_LETTER
%import common.LETTER -> LETTER
%import common.DIGIT  -> DIGIT
%import common.NUMBER
%import common.WS_INLINE
%ignore WS_INLINE
